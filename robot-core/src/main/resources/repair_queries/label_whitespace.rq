# # Label Whitespace
#
# **Problem:** A label has leading or trailing whitespace. This may cause issues when trying to reference the entity by label.
#
# **Solution:** Remove the additional whitespace.

PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

DELETE {
  ?entity ?property ?value
} INSERT {
  ?entity ?property ?newValue
} WHERE {
  VALUES ?property { rdfs:label }
  ?entity ?property ?value .
  FILTER ( !isBlank(?entity) )
  FILTER ( REGEX(str(?value), "[\\s\r\n]+$") || REGEX(str(?value), "^[\\s\r\n]+") )
  BIND( REPLACE(REPLACE(?value,"^[\\s\r\n]+",""),"[\\s\r\n]+$","") AS ?newValue )
}